
(* @NESTEDCOMMENTS := 'Yes' *)
(* @PATH := '' *)
(* @OBJECTFLAGS := '0, 8' *)
(* @SYMFILEFLAGS := '2048' *)
PROGRAM PLC_PRG
VAR
	J: USINT; (* VAR USADA PARA LOOPS - 1 NIVEL*)
	K: USINT; (* VAR USADA PARA LOOPS - 2 NIVEL*)
	COUNT: USINT; (* VAR USADA PARA CONTAGENS *)
END_VAR
(* @END_DECLARATION := '0' *)
(*-------------------------------------------------------------------------*)
(*->PRIMEIRA INICIALIZACAO - executa toda vez que eh ligado - uma vez*)
IF PRIMEIRO_STEP = TRUE  THEN
	IF PRIMEIRO_START = TRUE THEN
		IF J < 2 THEN (*RODA DUAS VEZES A INICIALIZACAO EVITAR BUG*)
			INICIALIZA_VARS();
			CHANGE_SCREEN(ENABLE:=TRUE, VISU_NAME:='FIRST');
			J:= J +1;
		END_IF
		IF TELA_FIRST() = TRUE THEN
			PRIMEIRO_START := FALSE;
			CHANGE_SCREEN(ENABLE:=TRUE, VISU_NAME:='MAIN');
		END_IF
	ELSE
		PRIMEIRO_STEP := FALSE;
		SEGUNDO_STEP := TRUE;
		OITAVO_STEP := TRUE;
		TECLA_F[4] := TRUE; (*INICIA COM O PRIMEIRO MODELO SELECIONADO*)
	END_IF
END_IF

(*-------------------------------------------------------------------------*)
(*->CONFIGURA OS ELEMENTOS DA TELA MAIN*)
IF SEGUNDO_STEP = TRUE THEN
	(*ESCREVE OS MODELOS NA SESSÃO DE MODELOS DA IHM*)
	FOR J:=1 TO 4 BY 1 DO
		IF J+MODELOS_RANGE <= NUM_MODELOS THEN
			IHM_STRING_MODELOS[J]:=MODELOS[J+MODELOS_RANGE];
		ELSE
			IHM_STRING_MODELOS[J]:=0;
		END_IF
	END_FOR
	(*ESCONDE OU MOSTRA O MODELO[4] NA IHM*)
	IF NUM_MODELOS = 4 THEN
		IHM_ESCONDER_MODELOS[4]:=FALSE;
		IHM_ESCONDER_MODELO4BACK:=FALSE;
	ELSIF NUM_MODELOS > 4 THEN
		IHM_ESCONDER_MODELOS[4]:=TRUE;
		IHM_ESCONDER_MODELO4BACK:=TRUE;
	ELSE
		IHM_ESCONDER_MODELOS[4]:=FALSE;
		IHM_ESCONDER_MODELO4BACK:=FALSE;
	END_IF
	(*BLOQUEIA ENTRADA(edicao) DOS MODELOS NA IHM*)
	FOR J:=1 TO 4 BY 1 DO
		IHM_BLOQUEIA_MODELOS[J] := TRUE;
	END_FOR
	(*PARA DESBUGAR ERRO DO CLP - vai e volta*)
	CHANGE_SCREEN(ENABLE:=TRUE, VISU_NAME:='PASS');
	CHANGE_SCREEN(ENABLE:=TRUE, VISU_NAME:='MAIN');
	(*AVANÇA PARA PROXIMO PASSO*)
	SEGUNDO_STEP:=FALSE;
	TERCEIRO_STEP:=TRUE;
	OITAVO_STEP := TRUE;
END_IF

(*-------------------------------------------------------------------------*)
(*->VERIFICA FEEDBAK DO KAP*)
IF TERCEIRO_STEP = TRUE THEN
	IF KAP_IN = TRUE THEN	(*mudar quando for download para [= FALSE]*)
		IHM_STRING_TITLE := 'CONECTE';
		IHM_STRING_DESCRIPTION := 'KAP';
		(*DESATIVA|ESCONDE BOTOES DE CONTROLE*)
		IHM_ESCONDER_ZERAR:=TRUE;
		IHM_ESCONDER_RESET:=TRUE;
		IHM_ESCONDER_SETUP:=TRUE;
		(*PAUSA TUDO*)
		KAP_OLDSTATE := FALSE;
		BATENTE:=FALSE;
		QUINZE_STEP := TRUE;
		QUINTO_STEP:=FALSE;
		SEXTO_STEP:=FALSE;
		NONO_STEP :=FALSE;
		DECIMO_STEP := FALSE;
	ELSE (*SENAO CONTINUA COM A VIDA*)
		QUINTO_STEP := TRUE;
		SEXTO_STEP:=TRUE;
		IF KAP_OLDSTATE = FALSE THEN
			KAP_OLDSTATE := TRUE; (*KAP VOLTA A STATUS ANTERIOR como LIGADO - pois foi reconectado*)
			QUINZE_STEP:= FALSE;
			PRG_SENSORES.DESATIVAR_TODOS();
			PRG_PARAFUSA.DESATIVAR();
			Y:= 1; (* RESETA A ETAPA - REINICIA CICLO*)
			OITAVO_STEP := TRUE; (*verifica e bota para rodar monitoracao*)
		END_IF
	END_IF
END_IF

(*-------------------------------------------------------------------------*)
(*->CONTROLE DA TELA CREDITS*)
IF QUARTO_STEP = TRUE THEN
	IF TELA_CREDITS() = TRUE THEN
		CHANGE_SCREEN(ENABLE:=TRUE, VISU_NAME:='MAIN');
		QUARTO_STEP := FALSE;
		QUINTO_STEP := TRUE;
		SEXTO_STEP := TRUE;
		TERCEIRO_STEP := TRUE;
	END_IF
END_IF

(*-------------------------------------------------------------------------*)
(*->MONITORA TECLAS DE ESCOLHA DO MODELO*)
IF QUINTO_STEP = TRUE THEN
	(*VERIFICA TECLA F7 - Mostra mais modelos*)
	IF IHM_ESCONDER_MODELO4BACK = TRUE THEN
		IF IsKeyPressed(CODE_KEY_F[7],0,0) = TRUE OR TECLA_F[7] = TRUE THEN
			IF MODELOS_RANGE < NUM_MODELOS -3 THEN
				MODELOS_RANGE := MODELOS_RANGE + 3;
			ELSE
				MODELOS_RANGE := 0;
			END_IF
			SEGUNDO_STEP:=TRUE;
		END_IF
	END_IF
	(*VERIFICA [F4..F7] - SELECIONA O MODELO*)
	FOR J:=1 TO 4 BY 1 DO
		IF IsKeyPressed(CODE_KEY_F[J+3],0,0) = TRUE OR TECLA_F[J+3] = TRUE THEN
			IF IHM_ESCONDER_MODELOS[J] = FALSE AND IHM_STRING_MODELOS[J] <> 0 THEN
				MODELO_SELECIONADO := IHM_STRING_MODELOS[J];
				IHM_ESCONDER_MODELOS[IHM_X] := FALSE;
				IHM_ESCONDER_MODELOS[J] := TRUE;
				IHM_X := J;
				X:= IHM_X + MODELOS_RANGE;
				Y:= 1;
				BATENTE:=FALSE;
				OITAVO_STEP := TRUE;
				PRG_SENSORES.DESATIVAR_TODOS();
				PRG_PARAFUSA.DESATIVAR();
				EXIT; (*sai do laço de repetiçao imediatamente*)
			END_IF
		END_IF
	END_FOR
	(*DESTACA MODELO SELECIONADO NA IHM*)
	IF MODELO_SELECIONADO <> IHM_STRING_MODELOS[IHM_X] THEN
		IHM_ESCONDER_MODELOS[IHM_X] := FALSE;
	ELSE
		IHM_ESCONDER_MODELOS[IHM_X] := TRUE;
	END_IF
	(*RESETA TECLAS PARA EVITAR ERROS*)
	FOR J:=4 TO 7 BY 1 DO
		TECLA_F[J] := FALSE;
	END_FOR
END_IF

(*-------------------------------------------------------------------------*)
(*->MONITORA TECLAS DE CONTROLE*)
IF SEXTO_STEP = TRUE THEN
	(*VERIFICA TECLA F1 - ZERAR*)
	IF IsKeyPressed(CODE_KEY_F[1],0,0) = TRUE OR TECLA_F[1] = TRUE THEN
		IF IHM_ESCONDER_ZERAR = FALSE THEN
			(*zera o ciclo*)
			PRG_SENSORES.DESATIVAR_TODOS();
			PRG_PARAFUSA.DESATIVAR();
			Y:= 1;
			BATENTE:= FALSE;
			OITAVO_STEP := TRUE;
		END_IF
		TECLA_F[1] := FALSE;
	END_IF
	(*VERIFICA TECLA F2 - RESET*)
	IF IsKeyPressed(CODE_KEY_F[2],0,0) = TRUE OR TECLA_F[2] = TRUE THEN
		IF IHM_ESCONDER_RESET = FALSE THEN
			(*reseta do pause na etapa*)
			TERCEIRO_STEP := TRUE;
			QUINTO_STEP := TRUE;
			SEXTO_STEP := TRUE;
			REATIVAR_DEVICES();
			DECIMO_STEP := TRUE;
		END_IF
		TECLA_F[2] := FALSE;
	END_IF
	(*VERIFICA TECLA F3 - SETUP*)
	IF IsKeyPressed(CODE_KEY_F[3],0,0) = TRUE OR TECLA_F[3] = TRUE THEN
		IF IHM_ESCONDER_SETUP = FALSE THEN
			(*desliga tudo*)
			TERCEIRO_STEP := FALSE;
			QUINTO_STEP := FALSE;
			SEXTO_STEP := FALSE;
			(*GERA A LETRA DA SENHA E VAI PARA TELA PASS*)
			IF RANDOM_NUM1 = 10  THEN
				LETRA_ALEATORIA := 'A';
			ELSIF RANDOM_NUM1 = 11 THEN
				LETRA_ALEATORIA := 'B';
			ELSIF RANDOM_NUM1 = 12 THEN
				LETRA_ALEATORIA := 'C';
			ELSIF RANDOM_NUM1 = 13 THEN
				LETRA_ALEATORIA := 'D';
			ELSIF RANDOM_NUM1 = 14 THEN
				LETRA_ALEATORIA := 'E';
			ELSIF RANDOM_NUM1 = 15 THEN
				LETRA_ALEATORIA := 'F';
			END_IF
			IHM_STRING_CHAVE:= CONCAT(USINT_TO_STRING(RANDOM_NUM2),LETRA_ALEATORIA);
			IHM_INT_SENHA := 0;
			SENHA_GERADA := RANDOM_NUM2 * RANDOM_NUM1;
			CHANGE_SCREEN(ENABLE:=TRUE, VISU_NAME:='PASS');
			TREZE_STEP := TRUE; (*HABILITA CONTROLE DA TELA PASS*)
		END_IF
		TECLA_F[3] := FALSE;
	END_IF
	(*VERIFICA TECLA ZERO[0] - MODO EDICAO*)
	IF IsKeyPressed(CODE_KEY_ZERO,0,0) = TRUE OR TECLA_ZERO = TRUE THEN
		IHM_STRING_TITLE := 'MODO';
		IHM_STRING_DESCRIPTION := 'EDIÇÃO';
		IHM_ESCONDER_MODELOS[IHM_X] := FALSE; (*DESELECIONA O MODELO ATUAL*)
		(*desliga tudo e zera*)
		TERCEIRO_STEP := FALSE;
		QUINTO_STEP := FALSE;
		SEXTO_STEP := FALSE;
		NONO_STEP := FALSE;
		DECIMO_STEP := FALSE;
		QUINZE_STEP := TRUE;
		QUATORZE_STEP := TRUE;
		(*HABILITA ENTRADA(edicao) DOS MODELOS*)
		FOR J:=1 TO 4 BY 1 DO
			IF IHM_STRING_MODELOS[J] <> 0 THEN
				IHM_BLOQUEIA_MODELOS[J] := FALSE;
			END_IF
		END_FOR
		IF IHM_ESCONDER_MODELO4BACK = TRUE THEN
			IHM_BLOQUEIA_MODELOS[4] := TRUE;
		END_IF
		(*PARA DESBUGAR ERRO DO CLP - vai e volta*)
		CHANGE_SCREEN(ENABLE:=TRUE, VISU_NAME:='PASS');
		CHANGE_SCREEN(ENABLE:=TRUE, VISU_NAME:='MAIN');
		TECLA_ZERO := FALSE;
	END_IF
	(*VERIFICA TECLA[- +/.] - TELA CREDITS*)
	IF IsKeyPressed(CODE_KEY_DOT,0,0) = TRUE OR TECLA_DOT = TRUE THEN
		CHANGE_SCREEN(ENABLE:=TRUE, VISU_NAME:='CREDITS');
		TERCEIRO_STEP := FALSE;
		QUARTO_STEP := TRUE;
		QUINTO_STEP := FALSE;
		SEXTO_STEP := FALSE;
		TECLA_DOT := FALSE;
	END_IF
END_IF

(*-------------------------------------------------------------------------*)
(*->CONTROLE DA TELA SETUP*)
IF SETIMO_STEP = TRUE THEN
	IF TELA_SETUP() = TRUE THEN
		(*volta para tela main - volta ciclo*)
		CHANGE_SCREEN(ENABLE:=TRUE, VISU_NAME:='MAIN');
		SETIMO_STEP:=FALSE;
		TERCEIRO_STEP := TRUE;
		QUINTO_STEP := TRUE;
		SEXTO_STEP := TRUE;
		OITAVO_STEP:= TRUE;
	END_IF
	(*VERIFICA TECLA[- +/.] - TELA FIRST*)
	IF IsKeyPressed(CODE_KEY_DOT,0,0) = TRUE OR TECLA_DOT = TRUE THEN
		(*vai para controle da tela FIRST*)
		CHANGE_SCREEN(ENABLE:=TRUE, VISU_NAME:='FIRST');
		SETIMO_STEP := FALSE;
		DOZE_STEP := TRUE;
		TECLA_DOT:= FALSE;
	END_IF
END_IF

(*-------------------------------------------------------------------------*)
(*->VERIFICA MATRIZ ANTES DE INICIAR CICLO*)
IF OITAVO_STEP = TRUE THEN
	(*CONFERE SE MATRIZ ESTÁ ZERADA*)
	COUNT:=0;
	ERRO_MATRIZ := FALSE;
	FOR J:=1 TO NUM_ETAPAS BY 1 DO
		FOR K:=1 TO NUM_ATIVOS -1 BY 1 DO
			IF MATRIZ[X,J,K] = 0 THEN
				COUNT:= COUNT +1;
			END_IF
			IF NUM_PARAFUSA = 1 THEN
				IF MATRIZ[X,J,K] > NUM_SENSORES AND MATRIZ[X,J,K] <> 15 THEN
					ERRO_MATRIZ := TRUE;
				END_IF
			ELSE
				IF MATRIZ[X,J,K] > NUM_SENSORES THEN
					ERRO_MATRIZ := TRUE;
				END_IF
			END_IF
		END_FOR
	END_FOR
	(*SE TIVER ERRO NA MATRIZ - TOMA PROVIDENCIAS*)
	IF ERRO_MATRIZ = TRUE THEN (*CASO DE ERRO*)
		IHM_ESCONDER_ZERAR := TRUE;
		IHM_ESCONDER_RESET := TRUE;
		DECIMO_STEP := FALSE;
		NONO_STEP := FALSE;
		IHM_STRING_TITLE:= 'RECONFIGURAR';
		IHM_STRING_DESCRIPTION:= 'ERRO MATRIZ';
	ELSIF COUNT = (NUM_ETAPAS * (NUM_ATIVOS -1)) THEN (*CASO ZERADA*)
		IHM_ESCONDER_ZERAR := TRUE;
		IHM_ESCONDER_RESET := TRUE;
		DECIMO_STEP := FALSE;
		NONO_STEP := FALSE;
		IHM_STRING_TITLE:= 'CADASTRAR';
		IHM_STRING_DESCRIPTION:= 'MODELO';
	ELSE (*SENAO CONTINUA INICIO DO CICLO*)
		IHM_ESCONDER_ZERAR := FALSE;
		IHM_ESCONDER_RESET := FALSE;
		NONO_STEP:=TRUE;
	END_IF
	OITAVO_STEP := FALSE;
END_IF

(*-------------------------------------------------------------------------*)
(*ATIVA DEVICES PARA INICIAR ETAPA*)
IF NONO_STEP = TRUE THEN
	(*CASO NAO TIVER TERMINADO AS ETAPAS*)
	IF Y <= NUM_ETAPAS THEN
		FOR Z:=1 TO NUM_ATIVOS BY 1 DO
			ARRAY_DE_ATIVOS[Z] := MATRIZ[X,Y,Z];
		END_FOR
		TEM_PARAFUSA:= FALSE;
		FOR Z:=1 TO NUM_ATIVOS-1 BY 1 DO
			IF ARRAY_DE_ATIVOS[Z] <> 0 THEN
				IF ARRAY_DE_ATIVOS[Z] = CODE_PARAFUSA THEN
					PRG_PARAFUSA.ATIVAR();
					TEM_PARAFUSA := TRUE;
				ELSE
					PRG_SENSORES.ATIVAR();
				END_IF
			END_IF
		END_FOR
		(*ATIVOU - PASSA PARA MONITORACAO*)
		NONO_STEP := FALSE;
		DECIMO_STEP := TRUE;
	ELSE (*FIM DO CICLO - LIBERA BATENTE*)
		BATENTE := FALSE;
		ONZE_STEP := TRUE;
	END_IF
END_IF

(*-------------------------------------------------------------------------*)
(*MONITORACAO DOS ATIVOS*)
IF DECIMO_STEP = TRUE THEN
	PRONTO := 0;
	IHM_STRING_TITLE := 'ATIVOS';
	IHM_STRING_DESCRIPTION := '';
	FOR Z:=1 TO NUM_ATIVOS -1 BY 1 DO
		IF ARRAY_DE_ATIVOS[Z] <> 0 THEN
			IF ARRAY_DE_ATIVOS[Z] = CODE_PARAFUSA THEN
				IHM_STRING_DESCRIPTION := CONCAT(IHM_STRING_DESCRIPTION, ' P');
				IHM_STRING_DESCRIPTION := CONCAT(IHM_STRING_DESCRIPTION, USINT_TO_STRING(ARRAY_DE_ATIVOS[4]));
				PRG_PARAFUSA.TORQUE(); (*verifica sinal de torque*)
				IF PRG_PARAFUSA.ERRO = TRUE THEN (*caso erro - TorqueNok*)
					IHM_STRING_TITLE := '';
					IHM_STRING_DESCRIPTION := 'TORQUE NOK';
					BATENTE := TRUE;
					QUINZE_STEP := TRUE;
					EXIT;
				END_IF
			ELSE
				IHM_STRING_DESCRIPTION := CONCAT(IHM_STRING_DESCRIPTION, ' S');
				IHM_STRING_DESCRIPTION := CONCAT(IHM_STRING_DESCRIPTION, USINT_TO_STRING(ARRAY_DE_ATIVOS[Z]));
				PRG_SENSORES.READ_TODOS(); (*verifica sinal dos sensores*)
				IF PRG_SENSORES.ERRO = TRUE THEN (*caso pegou algura peça errada*)
					IHM_STRING_TITLE := 'PEÇA';
					IHM_STRING_DESCRIPTION := 'ERRADA';
					BATENTE := TRUE;
					QUINZE_STEP := TRUE;
					EXIT;
				END_IF
			END_IF
		ELSE
			PRONTO := PRONTO + 1;
		END_IF
	END_FOR
	(*SE TODOS ATIVOS FORAM CONFIRMADOS - PRONTO SERÁ 3*)
	IF PRONTO = 3 THEN
		Y := Y+1; (*proxima etapa*)
		DECIMO_STEP := FALSE;
		NONO_STEP := TRUE;
	END_IF
END_IF

(*-------------------------------------------------------------------------*)
(*TEMPORIZADOR FINAL DO CICLO*)
IF ONZE_STEP = TRUE THEN
	IHM_STRING_TITLE:= 'BATENTE';
	IHM_STRING_DESCRIPTION := 'LIBERADO';
	BATENTE := FALSE;
	TEMPO_TON1(IN:=NOT TEMPO_TON1.Q, PT:=TEMPO_FINAL_CICLO);
	IF TEMPO_TON1.Q = TRUE THEN
		Y:=1; (*zera para etapa 1*)
		ONZE_STEP := FALSE;
	END_IF
END_IF

(*-------------------------------------------------------------------------*)
(*CONTROLE DA TELA FIRST*)
IF DOZE_STEP = TRUE THEN
	IF TELA_FIRST() = TRUE THEN (*se apertar p/ Sair - volta para tela Main*)
		SEGUNDO_STEP:= TRUE;
		QUINTO_STEP := TRUE;
		SEXTO_STEP := TRUE;
		OITAVO_STEP:= TRUE;
		NONO_STEP := TRUE;
		DOZE_STEP:=FALSE;
		CHANGE_SCREEN(ENABLE:=TRUE, VISU_NAME:='MAIN');
		(*volta para modelo 1*)
		MODELOS_RANGE:= 0;
		TECLA_F[4] := TRUE;
	END_IF
END_IF

(*-------------------------------------------------------------------------*)
(*CONTROLE DA TELA PASS*)
IF TREZE_STEP = TRUE THEN
	IF SEGURANCA_ENABLED = TRUE THEN
		IF TELA_PASS() = TRUE THEN
			IF PASS_LIBERADO = TRUE THEN
				QUINZE_STEP := TRUE;
				NONO_STEP:= FALSE;
				DECIMO_STEP := FALSE;
				(*inicializa vars da tela setup*)
				IHM_INT_ETAPAS := 1;
				FOR J:=1 TO 4 BY 1 DO
				IHM_INT_ATIVOS[J] := MATRIZ[X, IHM_INT_ETAPAS, J];
				END_FOR
				CHANGE_SCREEN(ENABLE:=TRUE, VISU_NAME:='SETUP');
				SETIMO_STEP := TRUE;
			ELSE
				(*volta para o main*)
				CHANGE_SCREEN(ENABLE:=TRUE, VISU_NAME:='MAIN');
				TERCEIRO_STEP := TRUE;
				QUINTO_STEP := TRUE;
				SEXTO_STEP := TRUE;
				OITAVO_STEP:= TRUE;
			END_IF
			TREZE_STEP := FALSE;
		END_IF
	ELSE
		(*inicializa vars da tela setup*)
		IHM_INT_ETAPAS := 1;
		FOR J:=1 TO 4 BY 1 DO
			IHM_INT_ATIVOS[J] := MATRIZ[X, IHM_INT_ETAPAS, J];
		END_FOR
		CHANGE_SCREEN(ENABLE:=TRUE, VISU_NAME:='SETUP');
		SETIMO_STEP := TRUE;
		TREZE_STEP := FALSE;
	END_IF
END_IF

(*-------------------------------------------------------------------------*)
(*CONTROLE EDICAO NOME DOS MODELOS*)
IF QUATORZE_STEP = TRUE THEN
	IF EDITA_MODELOS() = TRUE THEN
		MODELO_SELECIONADO := MODELOS[X];
		(*volta para o main*)
		CHANGE_SCREEN(ENABLE:=TRUE, VISU_NAME:='MAIN');
		REATIVAR_DEVICES();
		SEGUNDO_STEP := TRUE;
		TERCEIRO_STEP := TRUE;
		QUINTO_STEP := TRUE;
		SEXTO_STEP := TRUE;
		DECIMO_STEP := TRUE;
		QUATORZE_STEP := FALSE;
	END_IF
END_IF

(*-------------------------------------------------------------------------*)
(*CASO DE EM RESET - peça errada - torqueNok - USADO PARA DESLIGAR TUDO TAMBEM*)
IF QUINZE_STEP = TRUE THEN
	PRG_SENSORES.DESATIVAR_TODOS();
	PRG_PARAFUSA.DESATIVAR();
	NONO_STEP:=FALSE;
	DECIMO_STEP:=FALSE;
	QUINZE_STEP := FALSE;
END_IF


(*FORA DOS STEPS ==========================================================*)
(*-------------------------------------------------------------------------*)
(*RESETA TECLAS PARA EVITAR ERRO EM TELAS*)
IF QUINTO_STEP = FALSE OR SEXTO_STEP = FALSE THEN
	IsKeyPressed(CODE_KEY_F[1],0,0);
	IsKeyPressed(CODE_KEY_F[2],0,0);
	IsKeyPressed(CODE_KEY_F[3],0,0);
	IsKeyPressed(CODE_KEY_F[4],0,0);
	IsKeyPressed(CODE_KEY_F[5],0,0);
	IsKeyPressed(CODE_KEY_F[6],0,0);
	IsKeyPressed(CODE_KEY_F[7],0,0);
	IsKeyPressed(CODE_KEY_DOT,0,0);
	IsKeyPressed(CODE_KEY_ZERO,0,0);
END_IF

(*-------------------------------------------------------------------------*)
(*GERA NUMEROS ALEATORIOS - usados para a Chave na tela PASS*)
TEMPO_TON2(IN:=NOT TEMPO_TON2.Q, PT:=T#99ms );
TEMPO_TON3(IN:=NOT TEMPO_TON3.Q, PT:=T#123ms );
IF TEMPO_TON2.Q = TRUE THEN
	IF RANDOM_NUM2 < 3 THEN
		RANDOM_NUM2 := RANDOM_NUM2 +1;
	ELSE
		RANDOM_NUM2 := 1;
	END_IF
END_IF
IF TEMPO_TON3.Q = TRUE THEN
	IF RANDOM_NUM1 < 15 THEN
		RANDOM_NUM1 := RANDOM_NUM1 +1;
	ELSE
		RANDOM_NUM1 := 10;
	END_IF
END_IF


(*code end-line*)
END_PROGRAM
ACTION	REATIVAR_DEVICES:
(*MANDA LIGAR CADA ATIVO COMO PARAFUSADEIRA OU SENSOR*)
TEM_PARAFUSA := FALSE;
FOR Z:=1 TO NUM_ATIVOS-1 BY 1 DO
	IF ARRAY_DE_ATIVOS[Z] <> 0 THEN
		IF ARRAY_DE_ATIVOS[Z] = CODE_PARAFUSA THEN
			PRG_PARAFUSA.ATIVAR();
			TEM_PARAFUSA:= TRUE; (* indica que tem parafusadeira nessa etapa *)
		ELSE
			PRG_SENSORES.ATIVAR();
		END_IF
	END_IF
END_FOR
END_ACTION

